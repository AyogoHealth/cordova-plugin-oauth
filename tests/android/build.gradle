/**
 * Copyright 2022 Ayogo Health Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.3'
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 35

    defaultConfig {
      namespace 'com.ayogo.cordova.oauth.tests'
      applicationId 'com.ayogo.cordova.oauth.tests'
      minSdkVersion 24
      targetSdkVersion 35
      versionCode 1
      versionName '1.0'
      testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
      debug {
        testCoverageEnabled true
      }
    }
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation 'org.apache.cordova:framework:[11.0.0,)'
    implementation 'androidx.browser:browser:[1.3.0,1.9.0)'

    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.mockito:mockito-inline:4.6.1'
    testImplementation 'org.json:json:[20220924,)'

    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'

    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}

task copyPluginSource(type: Copy) {
    from '../../src/android'
    into 'src/main/java/com/ayogo/cordova/oauth'
}

task copyExampleAssets(type: Copy) {
  from '../../example/platforms/android/app/src/main/assets'
  into 'src/main/assets'
}

task copyExampleResources(type: Copy) {
  from '../../example/platforms/android/app/src/main/res'
  into 'src/main/res'
}

preBuild.dependsOn(copyPluginSource)
preBuild.dependsOn(copyExampleAssets)
preBuild.dependsOn(copyExampleResources)
